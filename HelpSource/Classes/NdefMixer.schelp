class:: NdefMixer
summary:: mix control for an Ndef proxyspace
categories:: JITLib>GUI, Live Coding
related:: Classes/ProxyMixer, Classes/JITGui, Classes/NdefGui


description::

NdefMixer is nearly identical to link::Classes/ProxyMixer::, except that it looks at the proxyspaces for each server that code::Ndef.all:: contains, and that it can be set to always follow a specific server.

subsection:: First examples

code::
n = NdefMixer(s);
n.parent.alwaysOnTop_(true); // show mixer in front of IDE

// variant that follows specified server even after Ndef.clear:
n = NdefMixer.forServer(s);

s.boot;
// if you have JITLibExtensions installed, try ProxyMeter to see the proxy levels:
if (\ProxyMeter.asClass.notNil) { ProxyMeter.addMixer(n) };

"bcdefghijk".do { |k| Ndef(k.asSymbol).ar };

"lmnopqrtuvw".do { |k| Ndef(k.asSymbol).kr };

"abcdefghijk".do { |k| Ndef(k.asSymbol).playN };
(
Ndef(\aaaaaa, { |freq = 250, intv = 19, timescale = 1, curve = 0, loopnode = 0|
	var env = EnvGen.kr(
		Env({ 1.0.rand2 }!11, { 1.0.rand }!10, curve, releaseNode: 9, loopNode: loopnode),
		timeScale: timescale);
	var pitch = (env * [1, 0.33, -1] * intv).midiratio * freq;
	Splay.ar(Formant.ar(pitch, pitch.scramble * 2, pitch.scramble)) * 0.1;
}).play(vol: 0.25);
)

n.switchSize(2);
n.switchSize(1);
n.switchSize(0);

n.switchSize(2);
n.editGui.object_(Ndef(\aaaaaa));

NdefMixer(Server.internal, 24);
::

CLASSMETHODS::
method:: forServer
Make an NdefMixer that follows the specific server given. This becomes relevant when Ndef.clear is used:
code::
// NdefMixer as before
x = NdefMixer.new(s, 5);
// NdefMixer that has a server to follow
y = NdefMixer.forServer(s, 5).moveTo(10, 50);

Ndef(\foo, {SinOsc.ar}); // both show foo
// Clear all proxyspaces in Ndef.all, and replace with new ones:
Ndef.clear; // -> foo disappears on both.
// running foo it again, it does not show on x,
// because x is still holding on to its old proxyspace;
// it does show on y, because y follows its server:
Ndef(\foo, {SinOsc.ar});
x.server; // x does not have its server
y.server; // y does.
::

argument::server
the server to follow

argument::numItems
how many slots for proxies to make

argument::parent
which parent view or window to show the mixer on

argument::bounds
bounds within which to show the mixer

argument::makeSkip
flag whether to make a skipjack for this mixer

argument::options
options for configuring the mixer

INSTANCEMETHODS::

method:: server
setting this server veriable explicitly will tell the NdefMixer to follow this server even when its proxyspace changes, for example when Ndef.clear is used.

method:: object
obj can be a server name, a server, or a proxyspace.

